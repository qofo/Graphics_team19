<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" >
<title>Example1</title>

<script id="vertex-shader" type="x-shader/x-vertex">
attribute vec4 vPosition;
attribute vec3 vNormal;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat4 normalMatrix;

varying vec3 fNormal;
varying vec3 fPosition;

void main() {
    vec4 pos = modelViewMatrix * vPosition;
    fPosition = pos.xyz;
    fNormal = normalize((normalMatrix * vec4(vNormal, 0.0)).xyz);
    gl_Position = projectionMatrix * pos;
}

</script>

<script id="fragment-shader" type="x-shader/x-fragment">
// set precision to mediump
precision mediump float;

varying vec3 fNormal;
varying vec3 fPosition;

uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;
uniform vec4 lightPosition;
uniform float shininess;

void main() {
    vec3 N = normalize(fNormal);
    vec3 L;
    if (lightPosition.w == 0.0)
        L = normalize(lightPosition.xyz);
    else
        L = normalize(lightPosition.xyz - fPosition);

    vec3 E = normalize(-fPosition);
    vec3 H = normalize(L + E);

    float Kd = max(dot(N, L), 0.0);
    float Ks = pow(max(dot(N, H), 0.0), shininess);

    vec4 ambient = ambientProduct;
    vec4 diffuse = Kd * diffuseProduct;
    vec4 specular = (dot(N, L) < 0.0) ? vec4(0.0) : Ks * specularProduct;

    gl_FragColor = ambient + diffuse + specular;
    gl_FragColor.a = 1.0;
}
</script>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="example.js"></script>
<script type="text/javascript" src="modeling.js"></script>
</head>

<body>
<canvas id="gl-canvas" width="1024" height="1024">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
</body>
</html>
